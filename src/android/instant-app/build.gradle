apply plugin: 'com.android.application'

android {
    namespace cordovaConfig.PACKAGE_NAMESPACE
    compileSdkVersion 34
    buildToolsVersion "30.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 22
        applicationId cordovaConfig.PACKAGE_NAMESPACE
        targetSdkVersion 34
        versionCode getVersionCodeGenerated()
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
    }

   if (file('../release-signing.properties').exists()) {
       signingConfigs {
           release {
               // These must be set or Gradle will complain (even if they are overridden).
               keyAlias = ""
               keyPassword = ""
               storeFile = null
               storePassword = ""
           }
       }
       buildTypes {
           release {
               signingConfig signingConfigs.release
           }
       }
       addSigningProps('../release-signing.properties', signingConfigs.release)
    }

    if (file('../debug-signing.properties').exists()) {
        addSigningProps('../debug-signing.properties', signingConfigs.debug)
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.google.android.gms:play-services-instantapps:18.0.1"
}

static def getVersionCodeGenerated() {
    return (System.currentTimeMillis() / 1000).intValue()
}

def addSigningProps(propsFilePath, signingConfig) {
    def propsFile = file(propsFilePath)
    def props = new Properties()
    propsFile.withReader { reader ->
        props.load(reader)
    }

    def storeFile = new File(props.get('key.store') ?: privateHelpers.ensureValueExists(propsFilePath, props, 'storeFile'))
    if (!storeFile.isAbsolute()) {
        storeFile = RelativePath.parse(true, storeFile.toString()).getFile(propsFile.getParentFile())
    }
    if (!storeFile.exists()) {
        throw new FileNotFoundException('Keystore file does not exist: ' + storeFile.getAbsolutePath())
    }
    signingConfig.keyAlias = props.get('key.alias') ?: privateHelpers.ensureValueExists(propsFilePath, props, 'keyAlias')
    signingConfig.keyPassword = props.get('keyPassword', props.get('key.alias.password', signingConfig.keyPassword))
    signingConfig.storeFile = storeFile
    signingConfig.storePassword = props.get('storePassword', props.get('key.store.password', signingConfig.storePassword))
    def storeType = props.get('storeType', props.get('key.store.type', ''))
    if (!storeType) {
        def filename = storeFile.getName().toLowerCase()
        if (filename.endsWith('.p12') || filename.endsWith('.pfx')) {
            storeType = 'pkcs12'
        } else {
            storeType = signingConfig.storeType
        }
    }
    signingConfig.storeType = storeType
}